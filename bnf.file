<program> ::= <identifier> ":" <block> ENDP <identifier> "."
 
<block> ::= DECLARATIONS <declaration_block> CODE <statement_list> | CODE <statement_list>

<declaration_block> ::= <variables> OF TYPE <type> ";" | <variables> OF TYPE <type> ";" <declaration_block>
<variables> ::= <identifier> | <identifier> "," <variables>
 
<type> ::= CHARACTER | INTEGER  | REAL

<statement_list> ::= <statement> | <statement> ";" <statement_list>
 
<statement> ::= <assignment_statement> | <if_statement> | <do_statement> | <while_statement> | <for_statement> | <write_statement> | <read_statement>

<assignment_statement> ::= <expression> "->" <identifier> 

<if_statement> ::= IF <conditional> THEN <statement_list> ELSE <statement_list> ENDIF IF <conditional> THEN <statement_list> ENDIF

<do_statement> ::= DO <statement_list> WHILE <conditional> ENDDO

<while_statement> ::= WHILE <conditional> DO <statement_list> ENDWHILE

<for_statement> ::= FOR <identifier> IS <expression> BY <expression> TO <expression> DO <statement_list> ENDFOR

<write_statement> ::= WRITE "(" <output_list> ")" | NEWLINE

<read_statement> ::= READ "(" <identifier> ")"

<output_list> ::= <value> | <value> "," <output_list>

<conditional> ::= <expression> <comparator> <expression> | <expression> <comparator> <expression> AND <conditional> | <expression> <comparator> <expression> OR <conditional> | NOT <conditional>
  
<comparator> ::= "=" | "<>" | "<" | ">" | "<=" | ">="
 
<expression> ::= <term> | <term> "+" <expression> | <term> "-" <expression>
 
<term> ::= <value> | <value> "*" <term> | <value> "/" <term>
 
<value> ::= <identifier> | <constant> | "(" <expression> ")"

<constant> ::= <number_constant> | <character_constant>

<character_constant> ::= "'" <character> "'"

<number_constant> ::= <number> | <number> "." <number> | "-" <number> |  "-" <number> "." <number> | <number> "." <number>
<number> ::= <digit> | <number> <digit>
 
<identifier> ::= <character> | <identifier> <character> | <identifier> <digit> 
 
<character> ::= a | b | c | d | e | f | g | h | I | j | k | l | m | n | o | p | q | r | s | t | u | w | x | y | z |
				A | B | C | D | E | F | G | H | I | J | K | L | M | N | O | P | Q | R | S | T | U | W | X | Y | Z

<digit> ::= 0 | 1 | 2 | 3 | 4 | 5 | 6 | 7 | 8 | 9
